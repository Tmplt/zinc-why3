module Ast_opt

  use imp.Imp
  use bv_op.BV_OP
  use int.Int
  use state.State

  (* TODO: use imp_exn *)
  use imp_ex_assignment.Imp_exn

  (*
  val eq (a b: 'a) : bool
    ensures { result = (a = b) }
  *)

  let rec aexpr_eq (a b:aexpr)
    variant { a, b }
    ensures { result = (a = b) }
  =
    match (a, b) with
    | Anum n1, Anum n2 ->
      n1 = n2
    | Avar (Id id1), Avar (Id id2) ->
      id1 = id2
    | Aadd a1 a2, Aadd b1 b2 ->
      aexpr_eq a1 b1 && aexpr_eq a2 b2
    | Asub a1 a2, Asub b1 b2 ->
      aexpr_eq a1 b1 && aexpr_eq a2 b2
    | Aaddu a1 a2, Aaddu b1 b2 ->
      aexpr_eq a1 b1 && aexpr_eq a2 b2
    | Asubu a1 a2, Asubu b1 b2 ->
      aexpr_eq a1 b1 && aexpr_eq a2 b2
    | _, _ -> false
    end

  let rec bexpr_eq (a b:bexpr)
    variant { a, b }
    ensures { result = (a = b) }
  =
    match (a, b) with
    | Btrue, Btrue               -> true
    | Bfalse, Bfalse             -> true
    | Bnot b1, Bnot b2           ->
      bexpr_eq b1 b2
    | Beq a1 a2, Beq b1 b2       ->
      aexpr_eq a1 b1 && aexpr_eq a2 b2
    | Ble a1 a2, Ble b1 b2       ->
      aexpr_eq a1 b1 && aexpr_eq a2 b2
    | Band b11 b12, Band b21 b22 ->
      bexpr_eq b11 b21 && bexpr_eq b12 b22
    | _, _                       -> false
    end

  let rec opt_aexpr (e:aexpr) : aexpr
    variant { e }
    ensures { forall st. aeval st result = aeval st e }
    (*
    ensures {
      forall st.
      let (_, opt_nodecount) = aeval_ex st result in
      let (_, nodecount) = aeval_ex st e in
      opt_nodecount <= nodecount
    }
    *)
  =
    match e with
    | Anum n      -> Anum n
    | Avar x      -> Avar x

    (* Constant reductions *)
    | Aadd e1 e2  ->
      let e1' = opt_aexpr e1 in
      let e2' = opt_aexpr e2 in
      match e1', e2' with
      | Anum n1, Anum n2 -> Anum (n1 + n2)
      | _, _             -> Aadd e1' e2'
      end
    | Aaddu e1 e2 ->
      let e1' = opt_aexpr e1 in
      let e2' = opt_aexpr e2 in
      match e1', e2' with
      | Anum n1, Anum n2 -> Anum (bv_add n1 n2)
      | _, _             -> Aaddu e1' e2'
      end
    | Asub e1 e2  ->
      let e1' = opt_aexpr e1 in
      let e2' = opt_aexpr e2 in
      match e1', e2' with
      | Anum n1, Anum n2 -> Anum (n1 - n2)
      | _, _             ->
        if aexpr_eq e1' e2' then
          Anum 0
        else
          Asub e1' e2'
      end
    | Asubu e1 e2 ->
      let e1' = opt_aexpr e1 in
      let e2' = opt_aexpr e2 in
      match e1', e2' with
      | Anum n1, Anum n2 -> Anum (bv_sub n1 n2)
      | _, _             ->
        if aexpr_eq e1' e2' then
          Anum 0
        else
          Asubu e1' e2'
      end
    end

  let rec opt_bexpr (b:bexpr) : bexpr
    variant { b }
    ensures { forall st. beval st result = beval st b }

    (*
    ensures {
      forall st.
      let (_, opt_nodecount) = beval_ex st result in
      let (_, nodecount) = beval_ex st b in
      opt_nodecount <= nodecount
    }
    *)
  =
    match b with
    | Btrue  -> Btrue
    | Bfalse -> Bfalse

    | Bnot b ->
      let b' = opt_bexpr b in
      match b' with
      | Btrue  -> Bfalse
      | Bfalse -> Btrue
      | _      -> Bnot b'
      end
    | Beq a1 a2 ->
      let a1' = opt_aexpr a1 in
      let a2' = opt_aexpr a2 in
      (* TODO: optimize more? *)
      Beq a1' a2'
    | Ble a1 a2 ->
      let a1' = opt_aexpr a1 in
      let a2' = opt_aexpr a2 in
      (* TODO: optimize more? *)
      Ble a1' a2'
    | Band b1 b2 ->
      let b1' = opt_bexpr b1 in
      let b2' = opt_bexpr b2 in
      match b1', b2' with
      | Btrue, Btrue -> Btrue
      | Bfalse, _    -> Bfalse
      | _, Bfalse    -> Bfalse
      | _, _         ->
      if bexpr_eq b1' b2' then
        b1'
      else
        Band b1' b2'
      end
    end

  let rec opt_com (c:com) : com
    variant { c }
    ensures {
      forall st st'. ceval st result st' = ceval st c st'

      (*
      forall st.
      let (opt_st', opt_nodecount) = ceval_ex st result in
      let (st', nodecount) = ceval_ex st c in
      opt_st' = st' /\ opt_nodecount <= nodecount
      *)
    }
  =
    match c with
    | Cskip        -> Cskip

    | Cassign id a -> Cassign id (opt_aexpr a)
    | Cseq c1 c2   ->
      (* match for CSkip *)
      let c1' = opt_com c1 in
      let c2' = opt_com c2 in
      Cseq c1' c2'
    | Cif b c1 c2  ->
      let b' = opt_bexpr b in
      let c1' = opt_com c1 in
      let c2' = opt_com c2 in
      match b' with
      | Btrue  -> Cseq c1' Cskip
      | Bfalse -> Cseq c2' Cskip
      | _      -> Cif b' c1' c2'
      end
    | Cwhile b c   ->
      let b' = opt_bexpr b in
      let c' = opt_com c in
      match b' with
      | Bfalse -> Cskip
      | _      -> Cwhile b' c'
      end
    end

end

(* vim: syntax=ocaml shiftwidth=2 shiftwidth=2
*)
